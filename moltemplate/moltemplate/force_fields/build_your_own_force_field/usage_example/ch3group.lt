# This file contains a definition for the "CH3" molecular subunit.



import "oplsaa_simple.lt"   # Load the force field settings we will need



# Then define "CH3":


CH3 inherits OPLSAA {

  # atom-id  mol-id   atom-type  charge   x             y                z

  write("Data Atoms") {
    $atom:C  $mol:... @atom:CT3   0.0  0.000000     0.000000      0.000000
    $atom:H1 $mol:... @atom:HCal  0.0  0.000000     0.631044      0.892431
    $atom:H2 $mol:... @atom:HCal  0.0  0.000000     0.631044     -0.892431
    $atom:H3 $mol:... @atom:HCal  0.0 -0.892431    -0.631044      0.000000
  }

  write('Data Bond List') {
    $bond:CH1 $atom:C $atom:H1
    $bond:CH2 $atom:C $atom:H2
    $bond:CH3 $atom:C $atom:H3
  }

} # CH3















# Optional: Shift all the coordinates in the +Y direction by 0.4431163.
#           This way, the carbon atom is no longer located at 0,0,0, but the
#           axis of an alkane chain containing this monomer is at 0,0,0.
#           (This makes it more convenient to construct a polymer later.
#            If this is confusing, then simply add 0.4431163 to the Y 
#            coordinates in the "Data Atoms" section above.)

CH3.move(0,0.4431163,0)




######### (scratchwork calculations for the atomic coordinates) #########
# Lcc = 1.5350 # length of the C-C bond (Sp3)
# Lch = 1.0930 # length of the C-H bond
# theta=2*atan(sqrt(2)) # ~= 109.5 degrees = tetrahedronal angle (C-C-C angle)
# DeltaXc = Lcc*sin(theta/2)  # = 1.2533222517240594
# DeltaYc = Lcc*cos(theta/2)  # = 0.8862326632060754
#                             # 0.5*DeltaYc = 0.4431163
# DeltaZh = Lch*sin(theta/2)  # = 0.892431
# DeltaYh = Lch*cos(theta/2)  # = 0.631044
